repositories {
  mavenCentral()
  maven { url 'https://jitpack.io' }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
  compileSdkVersion 25
  buildToolsVersion "25.0.1"

  defaultConfig {
    applicationId "io.bffcorreia.kotlintemplate"
    minSdkVersion 17
    targetSdkVersion 25
    versionCode 1
    versionName "1.0"
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }

  // Share code between test environments
  sourceSets {
    String sharedTestDir = 'src/sharedTest/java'
    test {
      java.srcDir sharedTestDir
    }
    androidTest {
      java.srcDir sharedTestDir
    }
  }

  sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
  }
}

kapt {
  generateStubs = true
}

dependencies {
  // Kotlin
  compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
  // Android
  compile 'com.android.support:appcompat-v7:25.1.0'
  compile 'com.android.support:cardview-v7:25.1.0'
  compile 'com.android.support:design:25.1.0'
  // Logging
  compile 'com.jakewharton.timber:timber:4.3.1'
  // Dependency Injection
  compile 'com.google.dagger:dagger:2.7'
  kapt 'com.google.dagger:dagger-compiler:2.7'
  // View Injection
  compile 'com.jakewharton:butterknife:8.4.0'
  kapt 'com.jakewharton:butterknife-compiler:8.4.0'
  // RxJava & RxAndroid
  compile 'io.reactivex:rxjava:1.2.3'
  compile 'io.reactivex:rxandroid:1.2.1'
  // Unit Tests
  testCompile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
  testCompile 'junit:junit:4.12'
  testCompile 'com.github.blocoio:faker:1.2.5'
  testCompile 'com.nhaarman:mockito-kotlin:1.0.1'
  testCompile 'org.assertj:assertj-core:3.6.1'
  // Instrumentation Tests
  androidTestCompile 'com.github.blocoio:faker:1.2.5'
  androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
    exclude group: 'com.android.support', module: 'support-annotations'
  })
}
